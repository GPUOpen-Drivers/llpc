; Make sure that the export channels match the location in the spir-v.
; This is how the PS knows which channel from which to read the value.

; BEGIN_SHADERTEST
; RUN: amdllpc -spvgen-dir=%spvgendir% -v %gfxip %s | FileCheck -check-prefix=SHADERTEST %s
; SHADERTEST-LABEL: _amdgpu_vs_main_fetchless:
; SHADERTEST: v_mov_b32_e32 [[zero:v[0-9]*]], 0
; SHADERTEST: v_mov_b32_e32 [[one:v[0-9]*]], 1.0
; SHADERTEST-DAG: exp param8 [[one]], [[zero]], [[zero]], [[one]]
; SHADERTEST-DAG: exp param7 [[zero]], [[zero]], [[one]], [[one]]
; SHADERTEST: AMDLLPC SUCCESS
; END_SHADERTEST

               OpCapability Shader
          %1 = OpExtInstImport "GLSL.std.450"
               OpMemoryModel Logical GLSL450
               OpEntryPoint Vertex %2 "main" %3 %gl_Position %5 %6 %7
          %8 = OpString ""
               OpDecorate %3 Location 1
               OpDecorate %3 Binding 0
               OpDecorate %gl_Position Binding 0
               OpDecorate %gl_Position BuiltIn Position
               OpDecorate %5 Location 5
               OpDecorate %5 Binding 0
               OpDecorate %6 Location 7
               OpDecorate %6 Binding 0
               OpDecorate %7 Location 8
               OpDecorate %7 Binding 0
       %void = OpTypeVoid
         %10 = OpTypeFunction %void
      %float = OpTypeFloat 32
    %v3float = OpTypeVector %float 3
    %v4float = OpTypeVector %float 4
%_ptr_Input_v4float = OpTypePointer Input %v4float
          %3 = OpVariable %_ptr_Input_v4float Input
%_ptr_Output_v4float = OpTypePointer Output %v4float
%gl_Position = OpVariable %_ptr_Output_v4float Output
%_ptr_Output_v3float = OpTypePointer Output %v3float
          %5 = OpVariable %_ptr_Output_v3float Output
          %6 = OpVariable %_ptr_Output_v4float Output
          %7 = OpVariable %_ptr_Output_v4float Output
         %17 = OpUndef %v3float
    %float_0 = OpConstant %float 0
    %float_1 = OpConstant %float 1
         %20 = OpConstantComposite %v4float %float_1 %float_0 %float_0 %float_1
         %21 = OpConstantComposite %v4float %float_0 %float_0 %float_1 %float_1
          %2 = OpFunction %void None %10
         %22 = OpLabel
         %23 = OpLoad %v4float %3
               OpStore %gl_Position %23
               OpStore %5 %17
               OpStore %6 %21
               OpStore %7 %20
               OpReturn
               OpFunctionEnd
